import org.gradle.api.tasks.testing.logging.TestExceptionFormat
import org.gradle.api.tasks.testing.logging.TestLogEvent;

allprojects {
    apply plugin: 'java'
    group = 'xyz.ressor'
    
    sourceCompatibility = 11

    dependencies {
        compileOnly group: 'org.jetbrains', name: 'annotations', version: '17.0.0'
        compile "org.slf4j:slf4j-api:$slf4jVersion"

        testCompile "org.apache.logging.log4j:log4j-slf4j-impl:$log4jVersion"
        testCompile "org.apache.logging.log4j:log4j-core:$log4jVersion"
        testCompile "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
        testCompile "org.junit.jupiter:junit-jupiter-api:$junitVersion"
        testCompile "org.mockito:mockito-junit-jupiter:$mockitoVersion"
        testCompile "org.assertj:assertj-core:$assertjVersion"
        testCompile "org.awaitility:awaitility:$awaitilityVersion"
    }

    repositories {
        mavenCentral()
        mavenLocal()
        jcenter()
    }

    javadoc {
        failOnError = false
    }

    test {
        forkEvery = 1
        maxParallelForks = Runtime.getRuntime().availableProcessors() * 4
        useJUnitPlatform()
        testLogging {
            events TestLogEvent.FAILED,
                    TestLogEvent.PASSED,
                    TestLogEvent.SKIPPED,
                    TestLogEvent.STANDARD_OUT
            exceptionFormat TestExceptionFormat.FULL
            showExceptions true
            showCauses true
            showStackTraces true

            debug {
                events TestLogEvent.STARTED,
                        TestLogEvent.FAILED,
                        TestLogEvent.PASSED,
                        TestLogEvent.SKIPPED,
                        TestLogEvent.STANDARD_ERROR,
                        TestLogEvent.STANDARD_OUT
                exceptionFormat TestExceptionFormat.FULL
            }
            info.events = debug.events
            info.exceptionFormat = debug.exceptionFormat

            afterSuite { desc, result ->
                if (!desc.parent) {
                    def output = "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
                    def startItem = '|  ', endItem = '  |'
                    def repeatLength = startItem.length() + output.length() + endItem.length()
                    println('\n' + ('-' * repeatLength) + '\n' + startItem + output + endItem + '\n' + ('-' * repeatLength))
                }
            }
        }
    }

    configurations.all {
        exclude group: "org.slf4j", module: "slf4j-log4j12"
        exclude module: "log4j-to-slf4j"
        exclude group: "ch.qos.logback"
    }
}

task aggregatedJavadocs(type: Javadoc, description: 'Generate javadocs from all child projects as if it was a single project', group: 'Documentation') {
    destinationDir = file("$buildDir/docs/javadoc")
    title = "$project.name $version API"
    failOnError = false
    options.author true
    options.links 'https://docs.oracle.com/en/java/javase/11/docs/api/index.html'
    options.addStringOption 'Xdoclint:none', '-quiet'

    subprojects.each { p ->
        p.tasks.withType(Javadoc).each { javadocTask ->
            source += javadocTask.source
            classpath += javadocTask.classpath
            excludes += javadocTask.excludes
            includes += javadocTask.includes
        }
    }
}